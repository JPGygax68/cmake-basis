##############################################################################
# @file  ExecutableTargetInfo.pm
# @brief Provides information about executables built by BASIS.
#
# Copyright (c) 2011 University of Pennsylvania. All rights reserved.
# See https://www.rad.upenn.edu/sbia/software/license.html or COPYING file.
#
# Contact: SBIA Group <sbia-software at uphs.upenn.edu>

package ExecutableTargetInfo;

use strict;
use warnings;

# ============================================================================
# exports
# ============================================================================

our ($VERSION, @ISA, @EXPORT, @EXPORT_OK, %EXPORT_TAGS);

BEGIN {
    use Exporter ();

    $VERSION = @PROJECT_VERSION_PERL@;
    @ISA     = qw (Exporter);

    %EXPORT_TAGS = (
        default => [qw (
            get_executable_name
            get_executable_directory
            get_executable_path
        )],

        everything => [qw (
            get_executable_name
            get_executable_directory
            get_executable_path
            get_target_uid
            is_known_target
        )]
    );

    Exporter::export_ok_tags ('everything');
}

# ============================================================================
# modules
# ============================================================================

use Cwd            qw(realpath);
use File::Basename qw(dirname basename);
use File::Spec     qw(rel2abs);

# ============================================================================
# private data
# ============================================================================

## @brief Default namespace used to make build target names fully-qualified.
my $_namespace = '@PROJECT_NAME_LOWER@';

## @brief Maps build target names to executable file paths relative to module.
my $_location = undef;

# ============================================================================
# public functions
# ============================================================================

##############################################################################
# @brief Get fully-qualified build target name.
#
# This function prepends the default namespace of used for targets build as
# part of the project this module belongs to if the given target name is yet
# neither known nor fully-qualified, i.e., in the form <namespace>::<target>.
#
# @note This function does not check whether the returned fully-qualified
#       target name is known to this class.
#
# @param [in] targe Name of build target.
#
# @returns Fully-qualified build target name.
sub get_target_uid
{
    my $target = $_[0];
    # invalid argument checks
    defined $target or return undef;
    length ($target) > 0 or return '';
    # initialize module if not done yet - this is only done here because
    # whenever information is looked up about an executable target, this
    # function is invoked first
    defined $_location or initialize ();
    # do nothing if target name is known
    not exists $_location->{$target} or return $target;
    # do nothing if target name is fully-qualified
    $target !~ /::/ or return $target;
    # otherwise, prepend default namespace name
    return $_namespace . '::' . $target;
}

##############################################################################
# @brief Determine whether a given target is known.
#
# @param [in] target Name/UID of build target.
#
# @returns Whether the given build target is known by this module.
sub is_known_target
{
    my $target = get_target_uid ($_[0]);
    defined $target or return 0;
    exists $_location->{$target};
}

##############################################################################
# @brief Get name part of executable file path.
#
# @param [in] target Name/UID of build target.
#
# @returns Name part of executable file path.
sub get_executable_name
{
    my $path = get_executable_path (@_);
    defined $path or return undef;
    return basename ($path);
}

##############################################################################
# @brief Get directory part of executable file path.
#
# @param [in] target Name/UID of build target.
#
# @returns Directory part of executable file path.
sub get_executable_directory
{
    my $path = get_executable_path (@_);
    defined $path or return undef;
    return dirname ($path);
}

##############################################################################
# @brief Get absolute file path of executable.
#
# @param [in] target Name/UID of build target.
#
# @returns Absolute file path of executable.
sub get_executable_path
{
    my $path = undef;
    if (@_ == 0) {
        $path = realpath ($0);
    } else {
        my $uid = get_target_uid ($_[0]);
        $path = $_location->{$uid};
        if (defined $path) {
            $path = File::Spec->rel2abs ($path, dirname (__FILE__));
        }
    }
    return $path;
}

# ============================================================================
# protected/private functions
# ============================================================================

##############################################################################
# @brief Initialize executable target information.
#
# This function initializes the structures of information about the executable
# build targets. In order to reduce the start-up time of applications that do
# not make use of this module, the initialization is only performed on demand.
#
# The initialization is done in get_target_uid() as this function is always
# called first before any lookup of information.
#
# @returns Nothing.
sub initialize
{
    $_location = {
    @EXECUTABLE_TARGET_INFO@
    };
}


1; # indicate success of module loading
